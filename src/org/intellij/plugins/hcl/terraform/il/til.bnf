{
//  org.intellij.plugins.hcl.terraform.il
//  parserUtilClass = "org.intellij.plugins.hcl.terraform.il.psi.TILParserUtil"
//  psiClassPrefix = 'TIL'


  elementTypeHolderClass = 'org.intellij.plugins.hcl.terraform.il.TILElementTypes'
  tokenTypeClass = 'org.intellij.plugins.hcl.terraform.il.TILTokenType'
  elementTypeClass = 'org.intellij.plugins.hcl.terraform.il.TILElementType'
  parserClass = 'org.intellij.plugins.hcl.terraform.il.TILParser'
  psiVisitorName = 'ILGeneratedVisitor'
  psiImplUtilClass = 'org.intellij.plugins.hcl.terraform.il.psi.impl.TILPsiImplUtilJ'

//  generateTokens=false
//  parserImports=[
//    "static org.intellij.plugins.hcl.terraform.il.TILTokenType.*"
//    "static org.intellij.plugins.hcl.terraform.il.TILParserUtil.*"
//  ]


  implements = 'org.intellij.plugins.hcl.terraform.il.psi.ILExpression'
  extends = 'org.intellij.plugins.hcl.terraform.il.psi.impl.ILExpressionBase'

  psiPackage = 'org.intellij.plugins.hcl.terraform.il.psi'
  psiImplPackage = 'org.intellij.plugins.hcl.terraform.il.psi.impl'

  extends(".*Expression|.*Variable|.*Variables")=ILExpression

  tokens = [
//    space = "regexp:\s+"
//    line_comment = "regexp:(//.*)|(#.*)"
//    block_comment = "regexp:/\*([^*]|\*[^/])*\*?(\*/)?"

//    L_BRACKET='['
//    R_BRACKET=']'
    INTERPOLATION_START='${'
    INTERPOLATION_END='}'
    L_PAREN='('
    R_PAREN=')'
    COMMA=','
    EQUALS='='
    POINT='.'

    // Operators
    OP_PLUS='+'
    OP_MINUS='-'
    OP_MUL='*'
    OP_DIV='/'
    OP_MOD='%'

    DOUBLE_QUOTED_STRING="regexp:\"([^\\\"\r\n]|\\[^\r\n])*\"?"
//    SINGLE_QUOTED_STRING="regexp:'([^\\\'\r\n]|\\[^\r\n])*'?"

    NUMBER='regexp:-?(0x)?(0|[1-9])\d*(\.\d+)?([eE][-+]?\d+)?'

    TRUE='true'
    FALSE='false'
    NULL='null'

    ID="regexp:[a-zA-Z\-_*][0-9a-zA-Z\-_*]*"
  ]

}


private root ::= ILExpression

// Expressions
ILExpression ::= ILParenthesizedExpression
  | ILExpressionHolder
  | ILSelectExpression
  | ILBinaryAddExpression
  | ILBinaryMulExpression
  | ILMethodCallExpression
  | ILLiteralExpression
  | ILVariable
{
  name="expression"
  implements="com.intellij.psi.templateLanguages.OuterLanguageElement"
  methods =[
    ILExpression="" // Magic hack
  ]
}

ILExpressionHolder ::= '${' ILExpression '}' {
  extends=ILExpression
}

ILParenthesizedExpression ::= '(' ILExpression ')'

ILBinaryAddExpression ::= ILExpression AddOp ILExpression
{
  implements="org.intellij.plugins.hcl.terraform.il.ILBinaryExpression"
  mixin="org.intellij.plugins.hcl.terraform.il.psi.impl.ILBinaryExpressionImpl"
}
ILBinaryMulExpression ::= ILExpression MulOp ILExpression
{
  implements="org.intellij.plugins.hcl.terraform.il.ILBinaryExpression"
  mixin="org.intellij.plugins.hcl.terraform.il.psi.impl.ILBinaryExpressionImpl"
}
ILMethodCallExpression ::= ILExpression ILParameterList
{
  implements="org.intellij.plugins.hcl.terraform.il.ILCallExpression"
  methods=[
    getQualifier
    getMethod
    getParameterList
  ]
}

ILLiteralExpression ::= literal /*| identifier*/ | number | 'true' | 'false' | 'null' {
   name="Literal"
   mixin="org.intellij.plugins.hcl.terraform.il.psi.impl.ILExpressionWithReference"
   methods=[ getTypeClass ]
}

ILParameterList ::= '(' (ILExpression? (',' ILExpression )* )?')' {
  pin(".*")=1
  methods=[ getParameters ]
}

ILSelectExpression ::= ILExpression DotOp ILVariable {
  methods=[
    from="ELExpression[0]"
    getField
  ]
}

ILVariable ::= identifier | '*' {
  name="Identifier"
  implements="com.intellij.psi.PsiNamedElement"
  mixin="org.intellij.plugins.hcl.terraform.il.psi.impl.ILExpressionWithReference"
  methods=[
    getName
    setName
    getUseScope
  ]
}

// Operators
private DotOp ::= '.'
private MulOp ::= '*' | '/' | '%'
private AddOp ::= '+' | '-'

// Primitives
private literal ::= DOUBLE_QUOTED_STRING
private identifier ::= ID
private number ::= NUMBER
