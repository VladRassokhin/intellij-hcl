{
  elementTypeHolderClass = 'org.intellij.plugins.hil.HILElementTypes'
  tokenTypeClass = 'org.intellij.plugins.hil.HILTokenType'
  elementTypeClass = 'org.intellij.plugins.hil.HILElementType'
  parserClass = 'org.intellij.plugins.hil.HILParser'
  psiVisitorName = 'ILGeneratedVisitor'
  psiImplUtilClass = 'org.intellij.plugins.hil.psi.impl.HILPsiImplUtilJ'

  implements = 'org.intellij.plugins.hil.psi.ILExpression'
  extends = 'org.intellij.plugins.hil.psi.impl.ILExpressionBase'

  psiPackage = 'org.intellij.plugins.hil.psi'
  psiImplPackage = 'org.intellij.plugins.hil.psi.impl'

  extends(".*Expression|.*Variable|ILExpressionHolder")=ILExpression

  tokens = [
//    space = "regexp:\s+"
    INTERPOLATION_START='${'
    INTERPOLATION_END='}'
    L_PAREN='('
    R_PAREN=')'
    L_BRACKET='['
    R_BRACKET=']'
    COMMA=','
    EQUALS='='
    POINT='.'

    // Operators
    OP_PLUS='+'
    OP_MINUS='-'
    OP_MUL='*'
    OP_DIV='/'
    OP_MOD='%'

    DOUBLE_QUOTED_STRING="regexp:\"([^\\\"\r\n]|\\[^\r\n])*\"?"

    NUMBER='regexp:(0x)?(0|[1-9])\d*(\.\d+)?([eE][-+]?\d+)?'

    TRUE='true'
    FALSE='false'
    NULL='null'

    ID="regexp:[a-zA-Z_][0-9a-zA-Z\-_*]*"
  ]

}


private root ::= ILExpression

// Expressions
// Update HILElementType.IL_EXPRESSIONS on change
ILExpression ::= ILParenthesizedExpression
  | ILExpressionHolder
  | ILIndexSelectExpression
  | ILSelectExpression
  | ILBinaryAddMulExpression
  | ILMethodCallExpression
  | ILLiteralExpression
  | ILUnaryExpression
  | ILVariable
{
  name="expression"
  implements="com.intellij.psi.templateLanguages.OuterLanguageElement"
  methods =[
    ILExpression="" // Magic hack
  ]
}

ILExpressionHolder ::= '${' ILExpression '}'

ILParenthesizedExpression ::= '(' ILExpression ')'

ILUnaryExpression ::= AddOp ILExpression
{
  methods = [
    operand="ILExpression"
    getOperationSign
  ]
}

// TODO: Split back into ILBinaryAddExpression and ILBinaryMulExpression when (if) normal operations order would be done in HIL
ILBinaryAddMulExpression ::= ILExpression (AddOp|MulOp) ILExpression
{
  implements="org.intellij.plugins.hil.ILBinaryExpression"
  mixin="org.intellij.plugins.hil.psi.impl.ILBinaryExpressionImpl"
}
ILMethodCallExpression ::= ILExpression ILParameterList
{
  methods=[
    Expression="ILExpression"
    ParameterList="ILParameterList"
    getQualifier
    getMethod
  ]
}

ILLiteralExpression ::= literal /*| identifier*/ | number | 'true' | 'false' | 'null' {
  name="Literal"
  mixin="org.intellij.plugins.hil.psi.impl.ILExpressionWithReference"
  methods=[
    getTypeClass
    getUnquotedText
  ]
}

ILParameterList ::= '(' (ILExpression? (',' ILExpression )* )?')' {
  pin(".*")=1
  methods=[
    ILExpression=""
    Parameters="ILExpression"
  ]
}

ILSelectExpression ::= ILExpression DotOp ILVariable {
  methods=[
    ILExpression=""
    from="ILExpression[0]"
    field="ILExpression[1]"
  ]
}

ILIndexSelectExpression ::= ILExpression '[' (ILExpression ']') {
  pin(".*")=2 // ¯\_(ツ)_/¯ Fixes testInceptionIndexes
  extends = ILSelectExpression
}

ILVariable ::= identifier | '*' {
  name="Identifier"
  implements="com.intellij.psi.PsiNamedElement"
  mixin="org.intellij.plugins.hil.psi.impl.ILVariableMixin"
}

// Operators
private DotOp ::= '.'
private MulOp ::= '*' | '/' | '%'
private AddOp ::= '+' | '-'

// Primitives
private literal ::= DOUBLE_QUOTED_STRING
private identifier ::= ID
private number ::= NUMBER
