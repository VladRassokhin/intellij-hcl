import groovy.json.JsonOutput
import groovy.json.JsonSlurper
import org.jsoup.Jsoup

/*
 * Copyright 2000-2017 JetBrains s.r.o.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
buildscript {
    ext.docBaseUrl='https://www.terraform.io'
    ext.jsoupVersion='1.9.2'
    ext.kotlinVersion='1.2.31'
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url 'http://repository.jetbrains.com/repo/'}
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}"
        classpath "org.jsoup:jsoup:${jsoupVersion}"
    }
}

plugins {
    id 'org.jetbrains.intellij' version '0.2.18'
}

version = '0.6.13'
if (System.hasProperty('build.number')) {
    version = "$version.${System.getProperty('build.number')}"
}
repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
    maven { url 'http://repository.jetbrains.com/repo/'}
}

apply plugin: 'org.jetbrains.intellij'
apply plugin: 'kotlin'
apply plugin: 'idea'

intellij {
    version '2018.1'
    pluginName 'intellij-hcl'
    sandboxDirectory="${project.projectDir}/idea-sandbox"
    updateSinceUntilBuild = false
    plugins = ["com.intellij.plugins.watcher:${getFileWatcherPluginVersion(version)}"]
}

test {
    systemProperty('idea.platform.prefix', 'Idea')
}

// To prevent weird classpath problems in idea
clean {
    delete "${intellij.sandboxDirectory}/plugins/${intellij.pluginName}"
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:${kotlinVersion}"
    compile 'com.beust:klaxon:0.31'
    testCompile 'org.assertj:assertj-core:2.6.0'
}

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

sourceSets {
    main {
        java {
            srcDirs 'src/java', 'gen'
        }
        kotlin {
            srcDir 'src/kotlin'
        }
        resources {
            srcDir 'res'
        }
    }
    test {
        java {
            srcDir 'test'
        }
        resources {
            srcDir 'test-data'
        }
    }
    integrationTest {
        kotlin {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('integration-test')
        }
    }
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:${kotlinVersion}"
    compile 'com.beust:klaxon:0.31'
    testCompile 'org.assertj:assertj-core:2.6.0'
    integrationTestCompile "org.jsoup:jsoup:${jsoupVersion}"
}
processResources {
    // Minify json resources
    doLast {
        fileTree(dir: outputs.files.asPath, include: "**/*.json").each {
            File file -> file.text = groovy.json.JsonOutput.toJson(new groovy.json.JsonSlurper().parse(file))
        }
    }
}

if (System.getenv('TEAMCITY_VERSION') != null) {
    test.ignoreFailures = true
} else {
//test.testLogging.showStandardStreams = true
}

idea {
    module {
        excludeDirs += file(intellij.sandboxDirectory)
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.5'
}

task integrationTest(type: Test) {
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
    outputs.upToDateWhen { false }
}

check.dependsOn integrationTest
integrationTest.mustRunAfter test

String getFileWatcherPluginVersion(String ideaVersion) {
    def version = GradleVersion.version(ideaVersion)

    if (version >= GradleVersion.version("2018.1") || ideaVersion.startsWith("181.")) {
        return "181.4096.12"
    }
    if (version >= GradleVersion.version("2017.3") || ideaVersion.startsWith("173.")) {
        return "173.3188.31"
    }
    if (version >= GradleVersion.version("2017.2") || ideaVersion.startsWith("172.")) {
        return "172.1909.7"
    }
    if (version >= GradleVersion.version("2017.1") || ideaVersion.startsWith("171.")) {
        return "171.1834.8"
    }
    if (version >= GradleVersion.version("2016.3") || ideaVersion.startsWith("163.")) {
        return "163.666"
    }
    if (version >= GradleVersion.version("2016.2") || ideaVersion.startsWith("162.")) {
        return "162.80"
    }
    return "143.110"
}

task fetchDocData {
    description "Reads from the Terraform website to update the doc-data-generated.json file (which " +
            "holds a small amount of information that cannot be derived from existing model data)"
    doLast {
        def docData = new JsonSlurper().parse(file('res/terraform/model-external/doc-data.json'))

        def docDataGenerated = [
                'functions': fetchFunctionDocData(),
                'paths': fetchProviderDocData(docData)
        ]

        def json = JsonOutput.prettyPrint(JsonOutput.toJson(docDataGenerated))

        file('res/terraform/model/doc-data-generated.json').write(json)
    }
}

def fetchFunctionDocData() {
    def functions = []

    def doc = Jsoup.connect("${docBaseUrl}/docs/configuration/interpolation.html").get()

    for (codeElement in doc.select("p > a[href^=#] > code")) {
        def signature = codeElement.text()
        def matcher = signature =~ /^(\w+)\(/
        if (matcher) {
            def name = matcher.group(1)
            functions.add([
                    "name": name,
                    "fragment": codeElement.parent().attr("href").substring(1),
                    "signature": signature
            ])
        }
    }

    return functions.sort { it[0] }
}

def fetchProviderDocData(docData) {
    def docDataPathsLookup = docData['paths'].collectEntries()
    def generatedPaths = []
    for (providerLink in Jsoup.connect("${docBaseUrl}/docs/providers/index.html").get().getElementsByTag("a")) {
        def providerMatch = providerLink.attr("href") =~ "/docs/providers/(\\w+)/index.html"
        if (providerMatch) {
            def provider = providerMatch.group(1)
            def keepProviderPrefix = docData['keepProviderPrefix'].contains(provider)
            def path = "/docs/providers/${docDataPathsLookup["provider.${provider}"] ?: provider}"
            def doc = Jsoup.connect("${docBaseUrl}${path}/index.html").get()
            for (link in doc.getElementsByTag("a")) {
                def matcher = link.attr("href") =~ "${path}/([dr])/(\\w+).html"
                if (matcher) {
                    def linkText = link.text()
                    def name = linkText.split(' ')[0]
                    def actualPage = matcher.group(2)
                    def expectedPage = keepProviderPrefix ? name : name.substring(name.indexOf('_') + 1)
                    if (actualPage != expectedPage) {
                        def type = matcher.group(1) == "d" ? "data" : "resource"
                        generatedPaths.add(["$type.$name", actualPage])
                    }
                }
            }
        }
    }

    return generatedPaths.sort { it[0] }
}
