/*
 * Copyright 2000-2016 JetBrains s.r.o.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
buildscript {
    ext.kotlinVersion='1.0.3'
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "http://repository.jetbrains.com/repo/"}
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}"
    }
}

plugins {
    id "org.jetbrains.intellij" version "0.1.10"
}

version = '0.5.9'
if (System.hasProperty('build.number')) {
    version = "$version.${System.getProperty("build.number")}"
}
repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
    maven { url "http://repository.jetbrains.com/repo/"}
}

apply plugin: 'org.jetbrains.intellij'
apply plugin: "kotlin"
apply plugin: 'idea'

intellij {
    version '14.1.7'
//    plugins 'coverage'
    pluginName 'intellij-hcl'
    sandboxDirectory="${project.projectDir}/idea-sandbox"
    updateSinceUntilBuild = false

    publish {
        pluginId '7808'
//        username ''
//        password ''
//        channel ''
    }
}

// To prevent weird classpath problems in idea
clean {
    delete "${intellij.sandboxDirectory}/plugins/${intellij.pluginName}"
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:${kotlinVersion}"
    compile 'com.beust:klaxon:0.25'
    compile('org.reflections:reflections:0.9.10') {
        exclude group: 'com.google.guava', module: 'guava'
    }
    testCompile 'org.assertj:assertj-core:2.5.0'
}

sourceCompatibility = "1.6"
targetCompatibility = "1.6"

sourceSets {
    main {
        java {
            srcDirs 'src/java', 'gen'
        }
        kotlin {
            srcDir 'src/kotlin'
        }
        resources {
            srcDir 'res'
        }
    }
    test {
        java {
            srcDir 'test'
        }
        resources {
            srcDir 'test-data'
        }
    }
}

if (System.getenv("TEAMCITY_VERSION") != null) {
    test.ignoreFailures = true
} else {
//test.testLogging.showStandardStreams = true
}

idea {
    module {
        excludeDirs += file(intellij.sandboxDirectory)
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.14.1'
}
